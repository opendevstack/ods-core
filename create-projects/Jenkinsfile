// BuildConfig environment variables
def odsNamespace = env.ODS_NAMESPACE ?: 'ods'
def odsImageTag = env.ODS_IMAGE_TAG ?: 'latest'
def odsGitRef = env.ODS_GIT_REF ?: 'master'
def odsBitbucketProject = env.ODS_BITBUCKET_PROJECT ?: 'opendevstack'
def projectId = env.PROJECT_ID
def projectAdmins = env.PROJECT_ADMIN ?: ''
def projectGroups = env.PROJECT_GROUPS ?: ''
def pipelineTriggerSecret = env.PIPELINE_TRIGGER_SECRET
def cdUserIdB64 = env.CD_USER_ID_B64
def cdUserType = env.CD_USER_TYPE

// Jenkins DeploymentConfig environment variables
def bitbucketUrl
def dockerRegistry
def pipelineOpenShiftProject
node {
  dockerRegistry = env.DOCKER_REGISTRY
  if (env.GIT_SERVER_URL) {
    bitbucketUrl = env.GIT_SERVER_URL
  } else if (env.BITBUCKET_URL) {
    bitbucketUrl = env.BITBUCKET_URL
  } else if (env.BITBUCKET_HOST) {
    bitbucketUrl = env.BITBUCKET_HOST // Fallback for Jenkins master instances not updated from 2.x
  } else {
    error('''Neither 'GIT_SERVER_URL' nor 'BITBUCKET_URL' is present, but at least one is required.''')
  }
  if (!bitbucketUrl.contains('://')) {
    bitbucketUrl = "https://${bitbucketUrl}" // Fallback for old provisioning app instances not updated from 2.x
  }
  echo("Resolved variable 'bitbucketUrl' to '${bitbucketUrl}'! [default=${env.BITBUCKET_URL}, custom=${env.GIT_SERVER_URL}]")
  pipelineOpenShiftProject = env.JOB_NAME.split('/').first()
}

def conts = containerTemplate(
  name: 'jnlp',
  image: "${dockerRegistry}/${odsNamespace}/jenkins-agent-base:${odsImageTag}",
  workingDir: '/tmp',
  alwaysPullImage: true,
  args: ''
)
def podLabel = "create-projects-${projectId}";

podTemplate(
  label: podLabel,
  cloud: 'openshift',
  containers: [conts],
  volumes: [],
  serviceAccount: 'jenkins'
) {
  node(podLabel) {
    currentBuild.description = "ODS Build #${env.BUILD_NUMBER} creating project: ${projectId}"
    stage('Checkout ods-core') {
      sh 'mkdir -p ods-core'
      dir('ods-core') {
        checkout scm
      }
    }

    stage('Checkout ods-configuration') {
      echo("Getting ods configuration from ${bitbucketUrl}")
      checkout([
        $class: 'GitSCM',
        branches: [[name: "*/master"]],
        doGenerateSubmoduleConfigurations: false,
        extensions: [[
          $class: 'RelativeTargetDirectory',
          relativeTargetDir: 'ods-configuration'
        ]],
        submoduleCfg: [],
        userRemoteConfigs: [[
          credentialsId: "${pipelineOpenShiftProject}-cd-user-with-password",
          url: "${bitbucketUrl}/scm/${odsBitbucketProject}/ods-configuration.git"
        ]]
      ])
    }

    stage('Create OpenShift projects') {
      dir('ods-core') {
        sh(
          script: """./create-projects/create-projects.sh --verbose \
            --project=${projectId} \
            --groups=${projectGroups}""",
          label: 'Create OpenShift projects'
        )
      }
    }

    stage('Setup Jenkins in CD project') {
      dir('ods-core') {
        sh(
          script: """./create-projects/create-cd-jenkins.sh --verbose \
            --non-interactive \
            --project=${projectId} \
            --ods-namespace=${odsNamespace} \
            --ods-image-tag=${odsImageTag} \
            --ods-bitbucket-project ${odsBitbucketProject} \
            --pipeline-trigger-secret-b64=${pipelineTriggerSecret} \
            --cd-user-type=${cdUserType} \
            --cd-user-id-b64=${cdUserIdB64}""",
          label: 'Setup Jenkins in CD project'
        )
      }
    }

    stage('Wait for Jenkins and Idle') {
      echo "Waiting for Jenkins to be up and running in project ${projectId}-cd..."
      sh(
        script: """
        set +e
        ready=0
        for i in {1..3}; do
          oc get pods -n ${projectId}-cd -l name=jenkins -o json | jq -e '
            .items[0].status.conditions
            | map(select((.type=="Ready" or .type=="ContainersReady") and .status=="True"))
            | length == 2
          ' && { ready=1; break; }
          echo "Jenkins not ready yet, waiting 15s..."
          sleep 15
        done
        set -e
        if [ "\$ready" -ne 1 ]; then
          echo "WARNING: Jenkins pod did not become ready after waiting. Attempting recovery..."
          oc rollout cancel dc/jenkins -n ${projectId}-cd || true
          oc scale dc/jenkins --replicas=0 -n ${projectId}-cd
          oc rollout latest dc/jenkins -n ${projectId}-cd
          oc scale dc/jenkins --replicas=1 -n ${projectId}-cd
          echo "Idling Jenkins deployment in project ${projectId}-cd..."
          oc idle -n ${projectId}-cd jenkins
        else
          echo "Idling Jenkins deployment in project ${projectId}-cd..."
          oc idle -n ${projectId}-cd jenkins
        fi
        """,
        label: 'Wait for Jenkins and Idle'
      )
    }
  }
}
